plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = "io.github.kolesnikovm"
version = '2.6.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def withoutBom = {
    exclude group: 'org.apache.jmeter', module: 'bom'
}

dependencies {
    compileOnly 'org.apache.jmeter:ApacheJMeter_core:5.4.1', withoutBom
    compileOnly 'org.apache.jmeter:ApacheJMeter_components:5.4.1', withoutBom
    implementation 'io.prometheus:simpleclient:0.9.0'
    implementation 'io.prometheus:simpleclient_servlet:0.9.0'
    implementation 'io.prometheus:simpleclient_hotspot:0.9.0'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.41.v20210516'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation "com.google.protobuf:protobuf-java:3.11.1"
    implementation "com.google.protobuf:protobuf-java-util:3.11.1"
    implementation "org.xerial.snappy:snappy-java:1.1.7.3"
    implementation "org.apache.httpcomponents:httpclient:4.5.12"
}

tasks.register('fatJar', Jar) {
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name

            artifact fatJar
            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'JMeter Prometheus Listener'
                description = 'Apache JMeter Backend Listener implementation for Prometheus metrics exporting.'
                url = 'https://github.com/kolesnikovm/jmeter-prometheus-listener'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kolesnikovm'
                        name = 'Maxim Kolesnikov'
                        email = 'maxxim.kolesnikov@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kolesnikovm/jmeter-prometheus-listener.git'
                    developerConnection = 'scm:git:ssh://github.com:kolesnikovm/jmeter-prometheus-listener.git'
                    url = 'https://github.com/kolesnikovm/jmeter-prometheus-listener'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = "$ossrhUsername"
                password = "$ossrhPassword"
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
